group = "com.bendb.droptools"
description = "blah"

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "nu.studer:gradle-jooq-plugin:3.0.3"
        classpath "com.github.jengelman.gradle.plugins:shadow:5.0.0"
    }
}

apply plugin: "application"
apply plugin: "nu.studer.jooq"
apply plugin: "com.github.johnrengelman.shadow"

uploadArchives.enabled = false

mainClassName = "com.bendb.example.ExampleApp"

run {
    args "server", "$projectDir/config/dev.yml"
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

dependencies {
    implementation project(":dropwizard-jooq")

    implementation libs.dropwizardCore
    implementation libs.dropwizardDb
    implementation dependencies.create("io.dropwizard.modules:dropwizard-flyway:0.9.2-3") {
        exclude module: "dropwizard-db"
    }
    implementation "com.google.auto.value:auto-value-annotations:1.6.2"
    annotationProcessor "com.google.auto.value:auto-value:1.6.2"

    implementation "org.postgresql:postgresql:42.2.4"
    
    jooqRuntime "org.postgresql:postgresql:42.2.4"
}

jooq {
    version = jooqVersion
    example(sourceSets.main) {
        jdbc {
            driver = "org.postgresql.Driver"
            url = "jdbc:postgresql://localhost:5432/example_app"
            user = "example_user"
            password = "s3cr3t"
        }

        generator {
            database {
                name = "org.jooq.meta.postgres.PostgresDatabase"
                inputSchema = "ex"
            }

            target {
                packageName = "com.bendb.example.db"
            }
        }
    }
}
