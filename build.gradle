allprojects {
    repositories {
        mavenCentral()
    }

    group GROUP
    version VERSION

    project.ext {
        dropwizardVersion = "3.0.0"
        jooqVersion = "3.16.18"
        jedisVersion = "3.0.1"
        postgresPluginVer = "42.6.0"

        libs = [
            testing: [
                "junit:junit:4.12",
                "org.mockito:mockito-all:1.10.19",
                "com.google.truth:truth:1.1.3",
            ],

            dropwizardDb: "io.dropwizard:dropwizard-db:$dropwizardVersion",
            jooq: "org.jooq:jooq:$jooqVersion",

            dropwizardCore: "io.dropwizard:dropwizard-core:$dropwizardVersion",
            jedis: "redis.clients:jedis:$jedisVersion",
        ]
    }
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

subprojects { sp ->
    apply plugin: "java-library"
    apply plugin: "idea"
    apply plugin: "jacoco"

    sourceCompatibility = "11"
    targetCompatibility = "11"

    tasks.withType(JavaCompile) {
        options.fork = true
        options.incremental = true
    }

    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showStackTraces true
            showExceptions true
            showCauses true
        }
    }
}

apply plugin: "jacoco"

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        // xml.enabled = true
        xml.destination file("$buildDir/reports/jacoco/report.xml")
        // html.enabled = true
        // csv.enabled = false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

// To accept gradle scans and publish them
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}
